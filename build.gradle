//file:noinspection GroovyAssignabilityCheck
plugins {
    id 'java'
    id 'edu.sc.seis.launch4j' version '2.5.0'
}

group 'net.jmb19905'
version '1.0.0-alpha4'

repositories {
    mavenCentral()
}

configurations {
    provided
    // Make compile extend from our provided configuration so that things added to bundled end up on the compile classpath
    implementation.extendsFrom(provided)
}

dependencies {
    provided 'io.netty:netty-all:4.1.66.Final'
    provided 'com.formdev:flatlaf:1.4'
    provided 'org.xerial:sqlite-jdbc:3.36.0.1'
    provided 'org.mindrot:jbcrypt:0.4'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.2'
}

task clientJar(type: Jar) {
    duplicatesStrategy = 'exclude'
    archiveName "ByteThrowMessenger-${project.version}-Client.jar"
    baseName "ByteThrowMessenger-${project.version}-Client"
    manifest {
        attributes 'Main-Class': 'net.jmb19905.client.ClientMain'
    }
    from configurations.provided.asFileTree.files.collect {
        exclude 'net/jmb19905/server**'
        zipTree(it)
    }
    with jar
}

task serverJar(type: Jar) {
    duplicatesStrategy = 'exclude'
    archiveName "ByteThrowMessenger-${project.version}-Server.jar"
    baseName "ByteThrowMessenger-${project.version}-Server"
    manifest {
        attributes 'Main-Class': 'net.jmb19905.server.StartServer'
    }
    from configurations.provided.asFileTree.files.collect {
        exclude 'net/jmb19905/client**'
        zipTree(it)
    }
    with jar
}

launch4j {
    outfile = "ByteThrowMessenger-${project.version}-Client.exe"
    mainClassName = 'net.jmb19905.networking.client.ClientMain'
    icon = "${projectDir}/build/libs/icon.ico"
    copyright = 'Copyright Â© Jared Bennett, 2021'
    fileDescription = 'A Simple Messenger'
    textVersion = '1.0.0-netty-alpha'
    internalName = 'ByteThrowMessenger'
    productName = "ByteThrowMessenger-${project.version}"
    jarTask = clientJar
}

//Don't create the default jar
jar.onlyIf { false }

task createExecutableFiles {
    dependsOn clientJar
    dependsOn serverJar
    dependsOn createAllExecutables
}

build.finalizedBy(createExecutableFiles)

test {
    useJUnitPlatform()
}