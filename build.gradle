//file:noinspection GroovyAssignabilityCheck
plugins {
    id 'java'
    id 'edu.sc.seis.launch4j' version '2.5.0'
}

group 'net.jmb19905'
version '1.0.0-alpha6'

repositories {
    mavenCentral()
}

configurations {
    provided
    implementation.extendsFrom(provided)
}

dependencies {
    provided 'com.formdev:flatlaf:1.6'
    provided 'org.xerial:sqlite-jdbc:3.36.0.2'
    provided 'org.mindrot:jbcrypt:0.4'
    provided 'com.fasterxml.jackson.core:jackson-core:2.13.0'
    provided 'com.fasterxml.jackson.core:jackson-annotations:2.13.0'
    provided 'com.fasterxml.jackson.core:jackson-databind:2.13.0'
    provided 'io.netty:netty-all:4.1.68.Final'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
}

task clientJar(type: Jar) {
    duplicatesStrategy = 'exclude'
    archiveName "ByteThrowMessenger-${project.version}-Client.jar"
    baseName "ByteThrowMessenger-${project.version}-Client"
    manifest {
        attributes 'Main-Class': 'net.jmb19905.bytethrow.client.ClientMain'
        attributes 'Implementation-Version': project.version
    }
    from configurations.provided.asFileTree.files.collect {
        exclude 'net/jmb19905/bytethrow/server**'
        exclude 'net/jmb19905/jmbnetty/server**'
        exclude 'version.properties'
        zipTree(it)
    }
    with jar
}

task serverJar(type: Jar) {
    duplicatesStrategy = 'exclude'
    archiveName "ByteThrowMessenger-${project.version}-Server.jar"
    baseName "ByteThrowMessenger-${project.version}-Server"
    manifest {
        attributes 'Main-Class': 'net.jmb19905.bytethrow.server.StartServer'
        attributes 'Implementation-Version': project.version
    }
    from configurations.provided.asFileTree.files.collect {
        exclude 'net/jmb19905/bytethrow/client**'
        exclude 'net/jmb19905/jmbnetty/client**'
        exclude 'version.properties'
        zipTree(it)
    }
    with jar
}

launch4j {
    outfile = "ByteThrowMessenger-${project.version}-Client.exe"
    mainClassName = 'net.jmb19905.networking.client.ClientMain'
    icon = "${projectDir}/build/libs/btm.ico"
    copyright = 'Copyright Â© Jared Bennett, 2021'
    fileDescription = 'A Simple Messenger'
    textVersion = project.version
    internalName = 'ByteThrowMessenger'
    productName = "ByteThrowMessenger-${project.version}"
    jarTask = clientJar
}

//Don't create the default jar
jar.onlyIf { false }

task createExecutableFiles {
    dependsOn clientJar
    dependsOn serverJar
    dependsOn createAllExecutables
}

build.finalizedBy(createExecutableFiles)

task createVersionFile(dependsOn: processResources) {
    doLast {
        new File("$buildDir/resources/main/version.properties").withWriter { w ->
            Properties p = new Properties()
            p['version'] = project.version.toString()
            p.store w, null
        }
    }
}

classes {
    dependsOn createVersionFile
}

test {
    useJUnitPlatform()
}