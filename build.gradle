plugins {
    id 'java'
    id 'edu.sc.seis.launch4j' version '2.4.9'
}

group 'net.jmb19905'
version '1.0.0-SNAPSHOT-6'

jar {
    archiveName "ByteThrowMessenger-${project.version}-Client.jar"
    baseName "ByteThrowMessenger-${project.version}-Client"
    manifest {
        attributes(
            'Main-Class': 'net.jmb19905.messenger.client.ByteThrowClient'
        )
    }
    from {
        compileJava.classpath.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
}

repositories {
    mavenCentral()
    maven {
        url "http://clojars.org/repo/"
    }
}

dependencies {
    implementation('com.esotericsoftware:kryonet:2.22.0-RC1')
    implementation('com.esotericsoftware:kryo:5.0.3')
    implementation('com.fasterxml.jackson.core:jackson-core:2.11.2')
    implementation('com.fasterxml.jackson.core:jackson-annotations:2.11.2')
    implementation('com.fasterxml.jackson.core:jackson-databind:2.11.2')
    implementation('org.xerial:sqlite-jdbc:3.32.3.2')
    implementation('org.mindrot:jbcrypt:0.4')
    implementation('com.formdev:flatlaf:0.46')
    //Lots of Utility Libraries... because why not?
    implementation('commons-io:commons-io:2.8.0')
    implementation('org.apache.commons:commons-lang3:3.11')
    implementation('org.apache.commons:commons-collections4:4.4')
    implementation('com.google.guava:guava:30.1-jre')
    //For testing
    implementation 'org.junit.jupiter:junit-jupiter:5.4.2'
}

test {
    useJUnitPlatform()
}

launch4j {
    outfile = "${project.name}-${project.version}.exe"
    mainClassName = 'net.jmb19905.messenger.client.ByteThrowClient'
    icon = "${projectDir}/build/libs/icon.ico"
    copyright = 'Copyright Â© Jared Bennett, 2021'
    fileDescription = 'A Simple Messenger'
    textVersion = '1.0.0-SN-5'
    internalName = 'ByteThrowMessenger'
    productName = "${project.name}-${project.version}"
}

task createVersion(){
    doLast {
        new File("${projectDir}/src/main/resources/", "version.txt").text = "${project.version}"
    }
}

task serverJar(type: Jar) {
    archiveName "BTM-${project.version}.jar"
    baseName "BTM-${project.version}"
    manifest {
        attributes(
            'Main-Class': 'net.jmb19905.messenger.server.ByteThrowServer'
        )
    }
    with jar
}